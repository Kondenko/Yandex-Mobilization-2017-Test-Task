apply plugin: "com.android.application"
apply plugin: 'realm-android'

android {
    compileSdkVersion 27
    buildToolsVersion "28.0.0"

    defaultConfig {
        applicationId "com.vladimirkondenko.yamblz"

        minSdkVersion 17
        targetSdkVersion 27

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file("release.properties")))

            storeFile rootProject.file(props.keyStore)
            keyAlias props.keyAlias
            keyPassword props.keyPassword
            storePassword props.storePassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release

            def configFiles = fileTree(dir: "./proguard", include: ["*.pro"]).asList()
            configFiles.add(getDefaultProguardFile('proguard-android.txt'))
            proguardFiles configFiles.toArray()
        }

        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            shrinkResources false
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        test.java.srcDirs += [generatedSourcesDir, 'src/testUtils/java']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties' // Fixes DuplicateFileException
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:2.0.1'
    }
}

tasks.withType(JavaCompile) {
    // Prevents the "endPosTable already set" exception
    // http://bugs.java.com/bugdatabase/view_bug.do?bug_id=8067747
    if (file(generatedSourcesDir).exists()) {
        FileCollection collection = files { file(generatedSourcesDir).listFiles() }
        collection.each { delete it }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    provided "org.glassfish:javax.annotation:10.0-b28"

    implementation "org.greenrobot:eventbus:$eventbus_version"
    implementation "io.realm:android-adapters:$realm_adapters_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    // Rx
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android_version"
    implementation "com.trello.rxlifecycle2:rxlifecycle:$rx_lifecycle_version"
    implementation "com.trello.rxlifecycle2:rxlifecycle-android:$rx_lifecycle_version"
    implementation "com.jakewharton.rxbinding2:rxbinding:$rx_bindings_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$rx_bindings_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rx_bindings_version"

    // Support
    implementation "com.android.support:support-v4:$support_lib_version"
    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:design:$support_lib_version"
    implementation "com.android.support:recyclerview-v7:$support_lib_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"

    // Testing
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: "com.android.support", module: "support-annotations"
    })

    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    testImplementation "com.google.dagger:dagger:$dagger_version"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    compile "com.facebook.stetho:stetho:$stetho_version"
    compile "com.facebook.stetho:stetho-okhttp3:$stetho_version"
    compile "com.uphyca:stetho_realm:$stetho_realm_version"
}
