apply plugin: "com.android.application"
apply plugin: "me.tatarka.retrolambda"
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.vladimirkondenko.yamblz"

        minSdkVersion 17
        targetSdkVersion 25

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            applicationIdSuffix ".release"
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        test.java.srcDirs += [generatedSourcesDir, 'src/testUtils/java']
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties' // Fixes DuplicateFileException
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:2.0.1'
    }
}

tasks.withType(JavaCompile) {
    // Prevents the "endPosTable already set" exception
    // http://bugs.java.com/bugdatabase/view_bug.do?bug_id=8067747
    if (file(generatedSourcesDir).exists()) {
        FileCollection collection = files { file(generatedSourcesDir).listFiles() }
        collection.each { delete it }
    }
}

dependencies {
    compile fileTree(dir: "libs", include: ["*.jar"])

    provided "org.glassfish:javax.annotation:10.0-b28"

    compile "org.greenrobot:eventbus:$eventbus_version"
    compile "io.realm:android-adapters:$realm_adapters_version"

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // Dagger
    compile "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    // RX
    compile "io.reactivex.rxjava2:rxandroid:$rx_android_version"
    compile "com.trello.rxlifecycle2:rxlifecycle:$rx_lifecycle_version"
    compile "com.trello.rxlifecycle2:rxlifecycle-android:$rx_lifecycle_version"
    compile "com.jakewharton.rxbinding2:rxbinding:$rx_bindings_version"
    compile "com.jakewharton.rxbinding2:rxbinding-support-v4:$rx_bindings_version"
    compile "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rx_bindings_version"

    // Support
    compile "com.android.support:support-v4:$support_lib_version"
    compile "com.android.support:appcompat-v7:$support_lib_version"
    compile "com.android.support:design:$support_lib_version"
    compile "com.android.support:recyclerview-v7:$support_lib_version"
    compile "com.android.support.constraint:constraint-layout:$constraint_layout_version"

    // Testing
    testCompile "junit:junit:$junit_version"
    testCompile "org.robolectric:robolectric:$robolectric_version"
    androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support", module: "support-annotations"
    })

    testCompile "org.mockito:mockito-core:$mockito_version"
    testCompile "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    testCompile "com.google.dagger:dagger:$dagger_version"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    compile "com.facebook.stetho:stetho:$stetho_version"
    compile "com.facebook.stetho:stetho-okhttp3:$stetho_version"
    compile "com.uphyca:stetho_realm:$stetho_realm_version"
}
